#
# Here, it's exactly the JAGS model used in `MORSE` package in the function `survFitTKTD()`
#
model {
  ######### priors
  kk_log10 ~ dnorm(kk_meanlog10, kk_taulog10)
  z_log10  ~ dnorm(z_meanlog10 , z_taulog10)
  kd_log10 ~ dnorm(kd_meanlog10, kd_taulog10)
  hb_log10 ~ dnorm(hb_meanlog10, hb_taulog10)

  ##### parameter transformation
  kk <- 10**kk_log10
  z  <- 10**z_log10
  ke <- 10**kd_log10
  hb <- 10**hb_log10

  ########## Computation of the likelihood

  for (gr in 1:n.group){

      for (t in 1:n.time){

          x.pos[gr,t] <- ifelse(x[gr,t] > 0, x[gr,t], 10) # Ensure positivity of x[i] for division, also : 1e^-10=0
          R[gr,t] <- ifelse(x[gr,t] > z, z/x.pos[gr,t], 0.1)
          t.z[gr,t] <- ifelse(x[gr,t] > z, -1/ke * log( 1- R[gr,t]), bigtime) # if z=0, t.z -> + infty
          #t.z[gr,t] <- ifelse(x[gr,t] > z, -1/ke * log( 1- z/x.pos[gr,t]), bigtime) # if z=0, t.z -> + infty

          # Integration between t.z and max(t)
          tref[gr,t] <- max(tprec[gr,t], t.z[gr,t])
          

          risk[gr,t] <- -hb * (time[gr,t] - tprec[gr,t]) +
                ifelse(time[gr,t] > t.z[gr,t],
                  -kk * ((x[gr,t] - z) * (time[gr,t] - tref[gr,t]) + x[gr,t]/ke * ( exp(-ke * time[gr,t]) - exp(-ke * tref[gr,t]))),
                  0
                )

          psurv[gr,t] <- exp(risk[gr,t])

          y[gr,t] ~ dbin(psurv[gr,t] , Nprec[gr,t])

      }
      
      #ysim[gr,1] ~ dbin(psurv[gr,1] , Nprec[gr,1])
      
      #log_lik[gr,1] <- (Nprec[gr,1]-y[gr,1])*log(1-psurv[gr,1]*1)
      #log_lik[gr,2] <- (Nprec[gr,2]-y[gr,2])*log(psurv[gr,1]*1-psurv[gr,2]*psurv[gr,1]) # psurv here is not the same as in IT, SD_Cext and IT_Cext model  
      
      #for( t in 2:n.time){
      #  ysim[gr,t] ~ dbin(psurv[gr,1] , ysim[gr,t-1])
      #}
      #for( t in 3:n.time){ # Ce n'est pas l'idéal dans le cas où n.time < 3
      #   log_lik[gr,t] <- (y[gr,t-1]-ysim[gr,t])*log(psurv[gr,t-1]*psurv[gr,t-2]-psurv[gr,t]*psurv[gr,t-1]) 
      #}
      #log_lik_gr[gr] <- sum(log_lik[gr,1:n.time])
  }
  #log_lik_Tot <- sum(log_lik_gr[1:n.group])
}
